//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
	dependencies {
		classpath "com.modrinth.minotaur:Minotaur:${libs.versions.minotaur.get()}"
		classpath "com.github.breadmoirai:github-release:${libs.versions.githubRelease.get()}"
	}
}


plugins {
	id 'maven-publish'
	alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = project.name
group = 'folk.sisby'

repositories {
	mavenLocal()
	maven { url "https://maven.terraformersmc.com/" }
	maven { url 'https://maven.ladysnake.org/releases' }
	maven { url "https://maven.nucleoid.xyz/" }
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url 'https://jitpack.io'
		content {
			includeGroupByRegex 'com\\.github\\..*'
		}
	}
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.yarn) { classifier "v2" }
	modImplementation libs.fl
	modImplementation libs.fapi

	modImplementation libs.styledNicknames
	modImplementation libs.playerData
	modImplementation libs.placeholder
}

processResources {
	final Map<String, String> map = [
		"version"        : version.toString(),
		"mc"             : libs.versions.mc.get(),
		"fl"             : libs.versions.fl.get(),
		"fapi"           : libs.versions.fapi.get(),
		"styledNicknames": libs.versions.styledNicknames.get()
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name "ProxyFox"
			url "https://maven.proxyfox.dev/"
			credentials(PasswordCredentials)
		}
	}
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'drogstyle'
		versionNumber = project.version
		versionName = baseVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = ['1.20', libs.versions.mc.get()]
		loaders = ['quilt', 'fabric', 'forge']
		detectLoaders = false
		dependencies {
			required.project "styled-nicknames"
		}
		changelog = rootProject.file("CHANGELOG.md").text + "<br/>\nChangelog: https://github.com/sisby-folk/drogstyle/releases/tag/v$baseVersion"
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "drogstyle"
		tagName "v$baseVersion"
		releaseName baseVersion
		targetCommitish defaultBranch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
		body rootProject.file("CHANGELOG.md").text
	}
	tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
	tasks.register("fullRelease") {
		group "publishing"
		dependsOn "githubRelease"
		dependsOn "modrinth"
	}
}
